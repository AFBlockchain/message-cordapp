apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.quasar-utils'

cordapp {
    targetPlatformVersion corda_platform_version
    minimumPlatformVersion corda_platform_version
    workflow {
        name "Message Flows"
        vendor "AFBlockchain"
        licence "Apache License, Version 2.0"
        versionId 1
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
    test {
        resources {
            srcDir rootProject.file("config/test")
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    // Corda dependencies.
    cordaCompile "$corda_core_release_group:corda-core:$corda_core_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"
    testCompile "$corda_release_group:corda-node-driver:$corda_release_version"

    // CorDapp dependencies.
    cordapp project(":contracts")
    cordapp files("$rootProject.projectDir/libs/corid-contracts-0.1.0.jar")
    cordapp files("$rootProject.projectDir/libs/corid-workflows-0.1.0.jar")

    testCompile "$accounts_release_group:accounts-contracts:$accounts_release_version"
    testCompile "$confidential_id_release_group:ci-workflows:$confidential_id_release_version"
    testCompile "$accounts_release_group:accounts-workflows:$accounts_release_version"
    testCompile 'com.github.manosbatsis.corda.testacles:corda-testacles-nodedriver:0.24'
    testCompile 'org.junit.jupiter:junit-jupiter:5.8.2'
}

task integrationTest(type: Test, dependsOn: []) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}